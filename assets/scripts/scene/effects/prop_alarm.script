
/**
 * Alarm prop effects.
 *
 * Supports the following input properties:
 * - "cfgAlarmFrequency": Frequency of the alarm blinking.
 * - "assetSndAlarm": Sound effect to play when blinking.
 */

var me             = self()
var blinkFrequency = $cfgAlarmFrequency ?? 0.5
var emissive       = color(1, 0.1, 0.05, 0.75)
var lightOffset    = vec3(0.1, 3.8, 0.1)
var lightRadiance  = color(1, 0.1, 0.05, 75)
var lightRadius    = 10

$lightAlarm ??= {
  var e = light_point_spawn(vec3(0, 0, 0), black, lightRadius)
  attach(e, me, null, lightOffset)
  destroy_after(e, me)
  e
}

var newBlinkOn = (round_nearest((time() * blinkFrequency)) % 2) == 1.0
if (newBlinkOn != $alarmBlinkOn) {

  renderable_param(me, "Emissive", newBlinkOn ? emissive : black)
  if (capable($lightAlarm, "Light")) {
    light_param($lightAlarm, "Radiance", newBlinkOn ? lightRadiance : black)
  }
  if (newBlinkOn && $assetSndAlarm) {
    var soundPos      = position(me)
    var soundGain     = 0.05
    var soundInstance = sound_spawn($assetSndAlarm, soundPos, soundGain)
    destroy_after(soundInstance, me)
    destroy_after(soundInstance, 3.0)
  }
}
$alarmBlinkOn = newBlinkOn


/**
 * Day-night cycle controller.
 *
 * Supports the following global input properties:
 * - "cfgDurationDay"  : How long the day lasts in seconds.
 * - "cfgDurationNight": How long the night lasts in seconds.
 *
 * Sets the following global properties:
 * - "cycleIsDay"   : Is it currently day or night.
 * - "cycleCounter" : Zero based index of the day.
 * - "cycleProgress": Normalized progress in the current day / night.
 */

var isDay    = global_load("cycleIsDay") ?? true
var counter  = global_load("cycleCounter") ?? 0
var progress = global_load("cycleProgress") ?? 0.5
var duration = (isDay ? global_load("cfgDurationDay") : global_load("cfgDurationNight")) ?? 30

if ((progress += time("Delta") / duration) >= 1.0) {
  isDay = !isDay
  if (isDay) {
    counter += 1
  }
  progress = 0.0
}

global_tell("cycleIsDay", isDay)
global_tell("cycleCounter", counter)
global_tell("cycleProgress", progress)

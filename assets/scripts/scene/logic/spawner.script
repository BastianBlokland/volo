/**
 * Every x seconds spawn a wave of enemies around this entity.
 *
 * Supports the following input properties:
 * - "cfgSpawnerPrefab": Prefab to spawn.
 */

var intervalMin   = 20
var intervalMax   = 60
var radius        = 20
var spawnCountMin = 50
var spawnCountMax = 150
var unitPrefab    = $cfgSpawnerPrefab ?? "InfantryMonster"

$nextWaveTime ??= time() + random_between(intervalMin, intervalMax)
if ($nextWaveTime <= time()) {
  $nextWaveTime = null

  var center     = position(self())
  var faction    = faction(self()) ?? "FactionB"
  var scale      = 1.0
  var spawnCount = round_nearest(random_between(spawnCountMin, spawnCountMax))

  for (var i = 0; i != spawnCount; i += 1) {
    var pos          = center + random_circle_xz() * radius
    var posUnblocked = nav_find(pos, "Normal", "UnblockedCell")
    var rot          = angle_axis(random() * pi * 2, up)

    prefab_spawn(unitPrefab, posUnblocked, rot, scale, faction)
  }
}

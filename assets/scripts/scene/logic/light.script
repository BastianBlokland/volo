
/**
 * Light that can dynamically change colors.
 *
 * Supports the following input properties:
 * - "cfgColor": Normal color.
 * - "cfgColorTrigger": Triggered color.
 * - "cfgTrigger": Entity to ask if we are triggered (will be asked for a 'triggered' property).
 */

var fixture           = self()
var lightRadius       = 5
var lightIntensity    = 3.5
var emissiveIntensity = 0.5
var lightOffset       = vec3(0.25, 0.5, 0)

if ($cfgTrigger) {
  ask(fixture, $cfgTrigger, "triggered")
}
var activeColor = $triggered ? $cfgColorTrigger : $cfgColor

var radiance = (activeColor ?? black) * lightIntensity
var emissive = (activeColor ?? black) * emissiveIntensity

renderable_param(fixture, "Emissive", emissive)
renderable_param(fixture, "Color", activeColor ?? white)

if ($light) {
  light_param($light, "Radiance", radiance)
} else {
  var e = light_point_spawn(vec3(0, 0, 0), black, lightRadius)
  attach(e, fixture, null, lightOffset)
  destroy_after(e, fixture)
  $light = e
}


var me = self()

if (active(me, "Dead")) {
  return
}

if ($cmdStop) {
  nav_stop(me)
  $cmdStop         = null
  $cmdMoveTarget   = null
  $cmdAttackTarget = null
  bark(me, "Confirm")
  return
}

if ($cmdMoveTarget) {
  if (nav_target(me) == $cmdMoveTarget) {
    // Already navigating to requested target; check if we arrived (having stopped travelling).
    if (!active(me, "Traveling")) {
      $cmdMoveTarget = null
    }
  } else {
    // Not yet navigating to requested target; start traveling.
    nav_travel(me, $cmdMoveTarget)
    attack(me, null)
    bark(me, "Confirm")
  }
  return
}

if (exists($cmdAttackTarget)) {
  if ($cmdAttackTarget != $lastRequestedTarget) {
    bark(me, "Confirm")
    $lastRequestedTarget = $cmdAttackTarget
  }
} else {
  $cmdAttackTarget     = null
  $lastRequestedTarget = null
}

var target = $cmdAttackTarget ?? target_primary(me)
if (target && health(target) > 0) {
  var losRadius = 0.5
  var los       = line_of_sight(me, target, losRadius)
  if (los != null && los <= target_range_max(me)) {
    nav_stop(me)
    attack(me, target)
  } else {
    if ($cmdAttackTarget) {
      // When its a user requested target we should move towards it if its not in rage.
      nav_travel(me, target)
    }
    attack(me, null)
  }
}
